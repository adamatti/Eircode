apply plugin: "groovy"
apply plugin: "idea"
apply plugin: "application"
apply plugin:"jacoco"

mainClassName = "adamatti.Main"

sourceSets {
    e2eTest {
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/e2e-test/groovy')
        }
        resources.srcDir file('src/e2e-test/resources')
    }
}

configurations {
    e2eTestCompile.extendsFrom testCompile
    e2eTestRuntime.extendsFrom testRuntime

    all*.exclude group: "org.slf4j", module: "slf4j-simple"
    all*.exclude group: "log4j", module: "log4j"
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7'
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.7'
    compile group: 'com.sparkjava', name: 'spark-core', version: '2.5.1'
    compile group: 'org.springframework', name: 'spring-context', version: '4.3.3.RELEASE'

    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    compile 'commons-io:commons-io:2.4'

    compile 'org.springframework.data:spring-data-redis:1.6.0.RELEASE'
    compile group: 'redis.clients', name: 'jedis', version: '2.8.1'

    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4-rc-2'
    testCompile group: 'cglib', name: 'cglib-nodep', version: '3.2.4' //required for mock
}

idea {
    module {
        downloadJavadoc = false
        downloadSources = false
    }
}

task e2eTest(type: Test) { // do not dependsOn='test' or you'll break IDEA debugger
    group = 'verification'
    testClassesDir = sourceSets.e2eTest.output.classesDir
    classpath = sourceSets.e2eTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

task stage(dependsOn: [build])

jacocoTestReport.dependsOn "test"
check.dependsOn "jacocoTestReport"
